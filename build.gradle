buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2'
    }
}

plugins {
    id "org.sonarqube" version "2.6.2"
}

apply plugin: 'java'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'jacoco'

group = 'de.kgrupp'
def isDebug = System.properties['inoks.java.monads.debug'] == "true"
def versionStr = System.properties['inoks.java.monads.version']
version = isDebug || !versionStr ? "LOCAL-VERSION" : versionStr

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile('org.junit.jupiter:junit-jupiter-api:5.2.0')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.2.0')
}

test {
    useJUnitPlatform()
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'inoks-java-monads'
        property 'sonar.projectName', 'inoks-java-monads'
    }
}

modifyPom {
    project {
        name 'inoks-java-monads'
        description 'Utility classes for developing with monads. '
        url 'https://github.com/kgrupp/inoks-java-monads'
        inceptionYear '2017'

        scm {
            url 'https://github.com/kgrupp/inoks-java-monads'
            connection 'scm:https://kgrupp@github.com/kgrupp/inoks-java-monads.git'
            developerConnection 'scm:git://github.com/kgrupp/inoks-java-monads.git'
        }

        licenses {
            license {
                name 'MIT License'
                url 'https://github.com/kgrupp/inoks-java-monads/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'kgrupp'
                name 'Konstantin Grupp'
                email 'coding@konstantin-grupp.de'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = project.hasProperty('nexusUrlRelease') ? nexusUrlRelease : 'http://localhost:8081/repository/maven-releases/'
    snapshotRepositoryUrl = project.hasProperty('nexusUrlSnapshot') ? nexusUrlSnapshot : 'http://localhost:8081/repository/maven-snapshots'
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}